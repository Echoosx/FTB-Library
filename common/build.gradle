dependencies {
	modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"

	modApi "dev.architectury:architectury:${rootProject.architectury_version}"

	modCompileOnly("me.shedaniel:RoughlyEnoughItems-api:${rootProject.rei_version}") {
		exclude group: "dev.architectury"
	}

	modCompileOnly("me.shedaniel:RoughlyEnoughItems-default-plugin:${rootProject.rei_version}") {
		exclude group: "dev.architectury"
	}
}

def ENV = System.getenv()

test {
	useJUnitPlatform()
}

architectury {
	common("forge", "fabric")
}

loom {
	accessWidenerPath = file("src/main/resources/ftblibrary.accesswidener")
}

configurations {
	dev
}

artifacts {
	dev(jar)
}

publishing {
	publications {
		mavenCommon(MavenPublication) {
			artifactId = rootProject.archivesBaseName
			from components.java
		}
	}

	repositories {
		if (ENV.FTB_MAVEN_TOKEN) {
			maven {
				url "https://maven.ftb.dev/releases"
				credentials {
					username = "ftb"
					password = "${ENV.FTB_MAVEN_TOKEN}"
				}
			}
		}

		if (ENV.SAPS_TOKEN) {
			maven {
				url "https://maven.saps.dev/releases"
				credentials {
					username = "ftb"
					password = "${ENV.SAPS_TOKEN}"
				}
			}
		}
	}
}
